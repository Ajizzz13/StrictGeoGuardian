plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.2.2'
}

group = 'com.zeroends'
version = '1.0.0'

java {
    toolchain.languageVersion = JavaLanguageVersion.of(21)
}

repositories {
    mavenCentral()
    maven { url = 'https://repo.purpurmc.org/snapshots' }
}

dependencies {
    compileOnly "org.purpurmc.purpur:purpur-api:1.21.9-R0.1-SNAPSHOT"
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'org.mindrot:jbcrypt:0.4'
}

processResources {
    filteringCharset = 'UTF-8'
    filesMatching('plugin.yml') {
        expand('version': project.version)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

shadowJar {
    archiveBaseName.set('StrictGeoGuardian')
    archiveClassifier.set('')
    archiveVersion.set(project.version)
    
    mergeServiceFiles()
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    
    relocate 'org.mindrot.jbcrypt', 'com.zeroends.strictgeoguardian.libs.bcrypt'
}
